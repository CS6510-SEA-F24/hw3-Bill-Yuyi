plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'jacoco'
}

group 'CS6510 - 24 fall'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    checkstyle 'com.puppycrawl.tools:checkstyle:10.3'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
    withJavadocJar()
}

checkstyle {
    toolVersion = '10.3'
    configFile = file('config/checkstyle/google_checks.xml')
    maxWarnings = 0
    ignoreFailures = false
}

// Requirement 2: Javadoc configuration
javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
    doLast {
        String fixedBuildDir = buildDir.toString().replace('\\', '/')
        println "Javadoc available at: file:///$fixedBuildDir/docs/javadoc/index.html"
    }
}

// Requirement 3: PMD configuration
pmd {
    toolVersion = '6.50.0'
    consoleOutput = true
    ignoreFailures = true
    rulesMinimumPriority = 5
    ruleSets = [
            "category/java/bestpractices.xml",
            "category/java/errorprone.xml",
            "category/java/codestyle.xml"
    ]
}

// Requirement 4: JaCoCo configuration
jacoco {
    toolVersion = "0.8.7"
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
        html.destination file("${buildDir}/jacocoHtml")
    }
    doLast {
        String fixedBuildDir = buildDir.toString().replace('\\', '/')
        println "Coverage report available at: file:///$fixedBuildDir/jacocoHtml/index.html"
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.8
            }
        }
    }
}

build {
    dependsOn jacocoTestReport
}

// Task to run all quality checks
task checkCode {
    dependsOn 'checkstyleMain'
    dependsOn 'checkstyleTest'
    dependsOn 'pmdMain'
    dependsOn 'pmdTest'
    dependsOn 'test'
    dependsOn 'jacocoTestReport'
    dependsOn 'jacocoTestCoverageVerification'
    dependsOn 'javadoc'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}